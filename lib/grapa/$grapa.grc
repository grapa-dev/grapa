/*
	Bootstrap for $RULE, which is used to define additional rules.
	The "grapa.grz" will autoload.
	Must be located in "lib/[appname]" directory where "lib" is easily findable.
*/

@global["$SPACE"] = token $SYM(" ",{$SKIP});
@global["$TAB"] = token $SYM("\t",{$SKIP});
@global["$CR"] = token $SYM("\r",{$SKIP});
@global["$LF"] = token $SYM("\n",{$SKIP});
@global["$UNKNOWN"] = token $OTHER("",{$SKIP});
@global["$BACKSLASH"] = token $SYSSYM("\\",{$SKIP,$ESC});
@global["$SLF"] = token $SYSSYM("\n",{$SKIP,$RUN});
@global["$BS"] = token $SYSSYM("[",{$START});
@global["$BE"] = token $SYSSYM("]",{$RUN,$END});
@global["$param_token"]
	= rule $SYSID("SYM") {@<lit,{$1}>}
	| $SYSID("INT") {@<lit,{$1}>}
	| $SYSID("RAW") {@<lit,{$1}>}
	| $SYSID("FLOAT") {@<lit,{$1}>}
	| $SYSID("ID") {@<lit,{$1}>}
	| $SYSID("STR") {@<lit,{$1}>}
	| $SYSID("SYSSYM") {@<lit,{$1}>}
	| $SYSID("SYSINT") {@<lit,{$1}>}
	| $SYSID("SYSID") {@<lit,{$1}>}
	| $SYSID("SYSSTR") {@<lit,{$1}>}
	| $SYSID("OTHER") {@<lit,{$1}>}
	;
@global["$param_value"]
	= rule '@' $ID {@<var,{$2}>}
	| <$op>
	| null {@<setnull,{}>}
	| $SYSSTR {@<lit,{$1}>}
	| $STR {@<lit,{$1}>}
	| $SYSID {@<lit,{$1}>}
	| $ID {@<lit,{$1}>}
	| $SYSINT {@<lit,{$1}>}
	| $INT {@<lit,{$1}>}
	| $RAW {@<lit,{$1}>}
	;
@global["$lib_start"]
	= rule <$command_list> ';' {@<lit,{$1}>}
	| <$command_list> {@<lit,{$1}>}
	;
@global["$list_value2"]
	= rule <$param_value>
	| '[' <$array_list> ']'
	| '{' <$lib_list> '}'
	;
@global["$list_value"]
	= rule $ID ':' <$list_value2> {@<name,{$1,$3}>}
	| $STR ':' <$list_value2> {@<name,{$1,$3}>}
	| <$list_value2>
	;
@global["$list_eqvalue"]
	= rule <$litname> '=' <$comp> {@<name,{$1,$3}>}
	| <$litname>
	;
@global["$attr"]
	= rule $SYSID("SKIP") {@<lit,{$1}>}
	| $SYSID("ESC") {@<lit,{$1}>}
	| $SYSID("RUN") {@<lit,{$1}>}
	| $SYSID("EXIT") {@<lit,{$1}>}
	| $SYSID("START") {@<lit,{$1}>}
	| $SYSID("END") {@<lit,{$1}>}
	| $SYSID("EMPTY") {@<lit,{$1}>}
	;
@global["$attr_list"]
	= rule <$attr> ',' <$attr_list> {@<prepend,{$3,$1}>}
	| <$attr> <$attr_list> {@<prepend,{$2,$1}>}
	| <$attr> {@<createlist,{$1}>}
	|
	;
@global["$lib_list"]
	= rule <$param_value> ',' <$lib_list> {@<prepend,{$3,$1}>}
	| <$param_value> '.' <$lib_list> {@<prepend,{$3,$1}>}
	| <$param_value> <$lib_list> {@<prepend,{$2,$1}>}
	| <$param_value> {@<createlist,{$1}>}
	|
	;
@global["$op"]
	= rule '@' '<' <$op_list> '>'
	| '@' '[' <$code_list> ']'
	| op '(' <$list_list> ')' '(' <$list_list> ')' {@<plan,{$6,$3}>}
	| op '(' <$list_list> ')' '{' <$lib_start> '}' {@<wrap,{$6,$3}>}
	| op '{' <$lib_start> '}' {@<wrap,{$3}>}
	;
@global["$op_list"]
	= rule <$list_value> ',' '[' <$array_list> ']' ',' '{' <$list_list> '}' {@<createop,{$1,$4,$8}>}
	| <$list_value> ',' '{' <$list_list> '}' {@<createop,{$1,$4}>}
	| <$list_value> ',' <$list_value> ',' '{' <$list_list> '}' {@<createop,{$1,$3,$6}>}
	| <$list_value> ',' <$list_value> {@<createop,{$1,$3}>}
	| <$list_value> {@<createop,{$1}>}
	;
@global["$code_list"]
	= rule ',' ',' <$code_list> {@<prepend,{$3,null,null}>}
	| ',' <$code_list> {@<prepend,{$2,null}>}
	| <$comp> ',' <$code_list> {@<prepend,{$3,$1}>}
	| <$comp> ',' {@<createcode,{$1,null}>}
	| <$comp> {@<createcode,{$1}>}
	| ',' {@<createcode,{null,null}>}
	;
@global["$array_list"]
	= rule <$list_value> ',' <$array_list> {@<prepend,{$3,$1}>}
	| <$list_value> {@<createarray,{$1}>}
	| {@<createarray,{}>}
	;
@global["$tuple_list"]
	= rule <$list_value> ',' <$tuple_list> {@<prepend,{$3,$1}>}
	| <$list_value> {@<createtuple,{$1}>}
	| {@<createtuple,{}>}
	;
@global["$list_list"]
	= rule <$list_value> ',' <$list_list> {@<prepend,{$3,$1}>}
	| <$list_value> {@<createlist,{$1}>}
	| {@<createlist,{}>}
	;
@global["$list_eqlist"]
	= rule <$list_eqvalue> ',' <$list_eqlist> {@<prepend,{$3,$1}>}
	| <$list_eqvalue> {@<createlist,{$1}>}
	| {@<createlist,{}>}
	;
@global["$token"]
	= rule '@' '@' $ID {@<var,{$3}>}
	| '@' '@' $SYSID {@<var,{$3}>}
	| '@' $ID {@<ref,{$2}>}
	| '@' $SYSID {@<ref,{$2}>}
	| '<' '>' {@<ruleref,{}>}
	| '<' <$op> '>' {@<ruleref,{null,$2}>}
	| '<' <$param_value> ',' <$op> '>' {@<ruleref,{$2,$4}>}
	| '<' <$param_value> '>' {@<ruleref,{$2}>}
	| '(' <$option_list> ')' {@<rule,{null,$2}>}
	| <$param_token> '(' <$param_value> ')' {@<ruleval,{$1,$3}>}
	| <$param_token> {@<ruleval,{$1}>}
	| $STR {@<ruleval,{$SYM,$1}>}
	| $ID {@<lit,{$1}>}
	| $SYSID {@<lit,{$1}>}
	| $INT {@<lit,{$1}>}
	| $RAW {@<lit,{$1}>}
	| $FLOAT {@<lit,{$1}>}
	;
@global["$token_list"]
	= rule <$token> <$token_list> {@<prepend,{$2,$1}>}
	| <$token> {@<createlist,{$1}>}
	;
@global["$option"]
	= rule <$token_list> '{' '}' {@<createlist,{$1}>}
	| <$token_list> '{' <$op> '}' {@<createlist,{$1,$3}>}
	| <$token_list> {@<createlist,{$1}>}
	| '{' '}' {@<createlist>}
	| '{' <$op> '}' {@<createlist,{null,$2}>}
	| {@<createlist>}
	;
@global["$option_list"]
	= rule <$option> '|' <$option_list> {@<prepend,{$3,$1}>}
	| <$option> {@<createrule,{$1}>}
	;
@global["$litname"]
	= rule '@' this {@<this,{}>}
	| '@' parent {@<parent,{}>}
	| '@' global {@<global,{}>}
	| '@' local {@<local,{}>}
	| '@' '<' <$op_list> '>'
	| '@' '[' <$code_list> ']'
	| '@' <$litname> {@<var,{$2}>}
	| this {@<this,{}>}
	| global {@<global,{}>}
	| local {@<local,{}>}
	| $this {@<this,{}>}
	| $parent {@<parent,{}>}
	| $global {@<global,{}>}
	| $local {@<local,{}>}
	| $root {@<var,{$1}>}
	| $self {@<var,{$1}>}
	| $ID {@<lit,{$1}>}
	| $SYSID {@<lit,{$1}>}
	| $STR {@<lit,{$1}>}
	| $SYSSTR {@<lit,{$1}>}
	| $INT {@<lit,{$1}>}
	| $RAW {@<lit,{$1}>}
	| $SYSINT {@<lit,{$1}>}
	| $FLOAT {@<lit,{$1}>}
	;
@global["$lit"]
	= rule '@' this {@<this,{}>}
	| '@' parent {@<parent,{}>}
	| '@' global {@<global,{}>}
	| '@' local {@<local,{}>}
	| '@' '<' <$op_list> '>'
	| '@' '[' <$code_list> ']'
	| '@' <$litname> {@<var,{$2}>}
	| this {@<this,{}>}
	| global {@<global,{}>}
	| local {@<local,{}>}
	| $this {@<this,{}>}
	| $parent {@<parent,{}>}
	| $global {@<global,{}>}
	| $local {@<local,{}>}
	| $root {@<var,{$1}>}
	| $self {@<var,{$1}>}
	| $ID {@<var,{$1}>}
	| $SYSID {@<lit,{$1}>}
	| $STR {@<lit,{$1}>}
	| $SYSSTR {@<lit,{$1}>}
	| $INT {@<lit,{$1}>}
	| $RAW {@<lit,{$1}>}
	| $SYSINT {@<lit,{$1}>}
	| $FLOAT {@<lit,{$1}>}
	;
@global["$param2"]
	= rule <$litname> ':' <$comp> {@<name,{$1,$3}>}
	| <$litname> '=' <$comp> {@<name,{$1,$3}>}
	| <$litname>
	;
@global["$list2"]
	= rule ',' ',' <$list2> {@<prepend,{$3,null,null}>}
	| ',' <$list2> {@<prepend,{$2,null}>}
	| <$param2> ',' <$list2> {@<prepend,{$3,$1}>}
	| <$param2> ',' {@<createlist,{$1,null}>}
	| <$param2> {@<createlist,{$1}>}
	| ',' {@<createlist,{null,null}>}
	;
@global["$param"]
	= rule <$litname> ':' <$comp> {@<name,{$1,$3}>}
	| <$litname> '=' <$comp> {@<name,{$1,$3}>}
	| <$comp>
	;
@global["$param_list"]
	= rule <$param> ',' <$param_list> {@<prepend,{$3,$1}>}
	| <$param> {@<createlist,{$1}>}
	|
	;
@global["$case"]
	= rule <$function> ':' <$command> ';' {@<case,{$1,$3}>}
	;
@global["$case_default"]
	= rule ':' <$command> ';' {@<case,{null,$2}>}
	;
@global["$case_rep"]
	= rule case <$case> <$case_rep> {@<prepend,{$3,$2}>}
	| case <$case> {@<createlist,{$2}>}
	;
@global["$case_list"]
	= rule <$case_rep> default <$case_default> {@<prepend,{$1,$3}>}
	| <$case_rep> {@<prepend,{$1,null}>}
	;
@global["$array"]
	= rule ',' ',' <$array> {@<prepend,{$3,null,null}>}
	| ',' <$array> {@<prepend,{$2,null}>}
	| <$comp> ',' <$array> {@<prepend,{$3,$1}>}
	| <$comp> ',' {@<createarray,{$1,null}>}
	| <$comp> {@<createarray,{$1}>}
	| ',' {@<createarray,{null,null}>}
	;
@global["$tuple"]
	= rule ',' ',' <$tuple> {@<prepend,{$3,null,null}>}
	| ',' <$tuple> {@<prepend,{$2,null}>}
	| <$comp> ',' <$tuple> {@<prepend,{$3,$1}>}
	| <$comp> ',' {@<createtuple,{$1,null}>}
	| <$comp> {@<createtuple,{$1}>}
	| ',' {@<createtuple,{null,null}>}
	;
@global["$list"]
	= rule ',' ',' <$list> {@<prepend,{$3,null,null}>}
	| ',' <$list> {@<prepend,{$2,null}>}
	| <$param> ',' <$list> {@<prepend,{$3,$1}>}
	| <$param> ',' {@<createlist,{$1,null}>}
	| <$param> {@<createlist,{$1}>}
	| ',' {@<createlist,{null,null}>}
	;
@global["$classlist"]
	= rule <$param> ';' <$classlist> {@<prepend,{$3,$1}>}
	| <$param> ';' {@<createlist,{$1}>}
	| <$param> {@<createlist,{$1}>}
	|
	;
@global["$elSelfClosingID"]
	= rule area {@<lit,{$1}>}
	| AREA {@<lit,{$1}>}
	| base {@<lit,{$1}>}
	| BASE {@<lit,{$1}>}
	| br {@<lit,{$1}>}
	| BR {@<lit,{$1}>}
	| col {@<lit,{$1}>}
	| COL {@<lit,{$1}>}
	| command {@<lit,{$1}>}
	| COMAND {@<lit,{$1}>}
	| embed {@<lit,{$1}>}
	| EMBED {@<lit,{$1}>}
	| hr {@<lit,{$1}>}
	| HR {@<lit,{$1}>}
	| img {@<lit,{$1}>}
	| IMG {@<lit,{$1}>}
	| input {@<lit,{$1}>}
	| INPUT {@<lit,{$1}>}
	| keygen {@<lit,{$1}>}
	| KEYGEN {@<lit,{$1}>}
	| link {@<lit,{$1}>}
	| LINK {@<lit,{$1}>}
	| meta {@<lit,{$1}>}
	| META {@<lit,{$1}>}
	| param {@<lit,{$1}>}
	| PARAM {@<lit,{$1}>}
	| source {@<lit,{$1}>}
	| SOURCE {@<lit,{$1}>}
	| track {@<lit,{$1}>}
	| TRACK {@<lit,{$1}>}
	| wbr {@<lit,{$1}>}
	| WBR {@<lit,{$1}>}
	;
@global["$elIDItem"]
	= rule $ID {@<lit,{$1}>}
	| $STR {@<lit,{$1}>}
	;
@global["$elID"]
	= rule <$elIDItem> ':' <$elIDItem> {@<add,{$1,":",$3}>}
	| <$elIDItem>
	;
@global["$elAttr"]
	= rule <$elID> '=' <$compL2> {@<name,{$1,$3}>}
	| <$elID>
	;
@global["$elAttrList"]
	= rule <$elAttr> <$elAttrList> {@<prepend,{$2,$1}>}
	| {@<createlist,{}>}
	;

@global["$elHtmlAttrBodyX"]
	= rule '>' <$htmlterm>
	| '>' <@<join,{$1}>> <$htmlterm> {@<prepend,{$3,$2}>}
	;
@global["$elHtmlValue"]
	= rule '/' '>' {@<createel,{}>}
	| <$elAttrList> '/' '>' {@<createel,{$1}>}
	| <$elAttrList> <$elHtmlAttrBodyX>  {@<createel,{$1,$2}>}
	;
@global["$elAttrBodyX"]
	= rule '>' <$xmlterm>
	| '>' <@<join,{$1}>> <$xmlterm> {@<prepend,{$3,$2}>}
	;
@global["$elValue"]
	= rule '/' '>' {@<createel,{}>}
	| <$elAttrList> '/' '>' {@<createel,{$1}>}
	| <$elAttrList> <$elAttrBodyX>  {@<createel,{$1,$2}>}
	;

@global["$elQname"]
	= rule '?' <$elID> {@<add,{"?",$2}>}
	;
@global["$elQvalue"]
	= rule <$elAttrList> {@<createel,{$1}>}
	;
	
@global["$elSkipValueY"]
	= rule '-' <@<join,{$1}>> ('-' '-' '>') {@<lit,{$2}>}
	;
@global["$elSkipValueX"]
	= rule <$elSkipValueY> {@<createxml,{$1}>}
	;
@global["$elSkipValue"]
	= rule <$elSkipValueX> {@<createel,{{},$1}>}
	;
	
@global["$tagDataValueY"]
	= rule '[' <@<join,{$1}>> (']' ']' '>') {@<lit,{$2}>}
	;
@global["$tagDataValueX"]
	= rule <$tagDataValueY> {@<createxml,{$1}>}
	;
@global["$tagDataValue"]
	= rule <$tagDataValueX> {@<createel,{{},$1}>}
	;

@global["$tagScriptValueY"]
	= rule '>' <@<join,{$1}>> ('<' '/' script '>') {@<name,{null,$2}>}
	;
@global["$tagScriptValueX"]
	= rule <$tagScriptValueY> {@<createxml,{$1}>}
	;
@global["$tagScriptValue"]
	= rule <$elAttrList> <$tagScriptValueX> {@<createel,{$1,$2}>}
	| <$elAttrList> '/' '>' {@<createel,{$1}>}
	;

@global["$tagDocHtmlValueY"]
	= rule <$litname> <$tagDocHtmlValueY> {@<prepend,{$2,$1}>}
	| <$litname> {@<createarray,{$1}>}
	;
@global["$tagDocHtmlValueX"]
	= rule html <$tagDocHtmlValueY> {@<prepend,{$2,$1}>}
	| html {@<createarray,{$1}>}
	| HTML <$tagDocHtmlValueY> {@<prepend,{$2,$1}>}
	| HTML {@<createarray,{$1}>}
	;
@global["$tagDocHtmlValue"]
	= rule <$tagDocHtmlValueX> {@<createel,{$1}>}
	;
@global["$tagDocValueX"]
	= rule <$litname> <$tagDocValueX> {@<prepend,{$2,$1}>}
	| <$litname> {@<createarray,{$1}>}
	;
@global["$tagDocValue"]
	= rule <$tagDocValueX> {@<createel,{$1}>}
	;

@global["$tagHtmlValue"]
	= rule '<' '!' '-' <$elSkipValue> {@<name,{"!--",$4}>}
	| '<' '!' '[' CDATA <$tagDataValue> {@<name,{"![CDATA[",$5}>}
	| '<' '!' DOCTYPE <$tagDocValue> '>' {@<name,{"!DOCTYPE",$4}>}
	| '<' '!' doctype <$tagDocValue> '>' {@<name,{"!DOCTYPE",$4}>}
	| '<' script <$tagScriptValue> {@<name,{"script",$3}>}
	| '<' <$elSelfClosingID> <$elQvalue> '/' '>' {@<name,{$2,$3}>}
	| '<' <$elSelfClosingID> <$elQvalue> '>' {@<name,{$2,$3}>}
	| '<' <$elQname> <$elQvalue> '?' '>' {@<name,{$2,$3}>}
	| '<' <$elID> <$elHtmlValue> {@<name,{$2,$3}>}
	;
@global["$tagHtmlcreate"]
	= rule <$tagHtmlValue> {@<createtag,{$1}>}
	;
@global["$tagHtmlHead"]
	= rule '<' '!' DOCTYPE <$tagDocHtmlValue> '>' {@<name,{"!DOCTYPE",$4}>}
	| '<' '!' doctype <$tagDocHtmlValue> '>' {@<name,{"!DOCTYPE",$4}>}
	;
@global["$tagValue"]
	= rule '<' '!' '-' <$elSkipValue> {@<name,{"!--",$4}>}
	| '<' '!' '[' CDATA <$tagDataValue> {@<name,{"![CDATA[",$5}>}
	| '<' '!' DOCTYPE <$tagDocValue> '>' {@<name,{"!DOCTYPE",$4}>}
	| '<' '!' doctype <$tagDocValue> '>' {@<name,{"!DOCTYPE",$4}>}
	| '<' script <$tagScriptValue> {@<name,{"script",$3}>}
	| '<' <$elSelfClosingID> <$elQvalue> '/' '>' {@<name,{$2,$3}>}
	| '<' <$elQname> <$elQvalue> '?' '>' {@<name,{$2,$3}>}
	| '<' <$elID> <$elValue> {@<name,{$2,$3}>}
	;
@global["$tagcreate"]
	= rule <$tagValue> {@<createtag,{$1}>}
	;

@global["$htmlterm"]
	= rule '<' '/' <$elID> '>' {@<createxml,{}>}
	| '<' '/' '>' {@<createxml,{}>}
	| '<' '>' {@<createxml,{}>}
	| <$tagHtmlcreate> <$htmlterm> {@<prepend,{$2,$1}>}
	| <$tagHtmlcreate> <@<join,{$1}>> <$htmlterm> {@<prepend,{$3,$2,$1}>}
	;
@global["$xmlterm"]
	= rule '<' '/' <$elID> '>' {@<createxml,{}>}
	| '<' '/' '>' {@<createxml,{}>}
	| '<' '>' {@<createxml,{}>}
	| <$tagcreate> <$xmlterm> {@<prepend,{$2,$1}>}
	| <$tagcreate> <@<join,{$1}>> <$xmlterm> {@<prepend,{$3,$2,$1}>}
	;
@global["$htmlcreate"]
	= rule '<' '/' '>' {@<createxml,{}>}
	| '<' '>' {@<createxml,{}>}
	| <$tagHtmlHead> <$htmlcreate> {@<prepend,{$2,$1}>}
	| <$tagHtmlHead> {@<createxml,{$1}>}
	| <$tagHtmlcreate> <$htmlcreate> {@<prepend,{$2,$1}>}
	| <$tagHtmlcreate> <@<join,{$1}>> <$htmlcreate> {@<prepend,{$3,$2,$1}>}
	| <$tagHtmlcreate> {@<createxml,{$1}>}
	;
@global["$xmlcreate"]
	= rule '<' '/' '>' {@<createxml,{}>}
	| '<' '>' {@<createxml,{}>}
	| <$tagHtmlHead> <$htmlcreate> {@<prepend,{$2,$1}>}
	| <$tagHtmlHead> {@<createxml,{$1}>}
	| <$tagcreate> <$xmlcreate> {@<prepend,{$2,$1}>}
	| <$tagcreate> <@<join,{$1}>> <$xmlcreate> {@<prepend,{$3,$2,$1}>}
	| <$tagcreate> {@<createxml,{$1}>}
	;
@global["$opcreate"]
	= rule '(' ')' '(' <$list> ')' {@<plan,{$4,@<createlist,{}>}>}
	| '(' ')' '{' <$lib_start> '}' {@<wrap,{$4,@<createlist,{}>}>}
	| '(' <$list2> ')' '(' <$list> ')' {@<plan,{$5,$2}>}
	| '(' <$list2> ')' '{' <$lib_start> '}' {@<wrap,{$5,$2}>}
	| '{' <$lib_start> '}' {@<wrap,{$2}>}
	;
@global["$lambdacreate"]
	= rule <$list_eqlist> ':' '{' <$lib_start> '}' {@<plan,{$4,$1}>}
	;
@global["$function"]
	= rule '[' ']' {@<createarray,{}>}
	| '[' <$array> ']'
	| '#' '[' ']' '#'  {@<vector,{}>}
	| '#' '[' <$array> ']' '#'  {@<vector,{$3}>}
	| '{' '}' {@<createlist,{}>}
	| '{' <$list> '}'
	| '(' ')'
	| '(' <$comp> ')' '(' ')' {@<call,{$2}>}
	| '(' <$comp> ')' '(' <$list> ')' {@<call,{$2,$5}>}
	| '(' <$comp> ')'
	| '(' <$tuple> ')'
	| rule <$option_list> {@<rule,{null,$2}>}
	| op <$opcreate>
	| lambda <$lambdacreate>
	| class '(' <$array> ')' '{' <$classlist> '}' {@<class,{null,$6,$3}>}
	| class '(' <$array> ')' {@<class,{null,null,$3}>}
	| class '{' <$classlist> '}' {@<class,{null,$3}>}
	| token <$param_token> '(' <$param_value> ',' '{' <$attr_list> '}' ')' {@<token,{$2,null,$4,$7}>}
	| token <$param_token> '(' <$param_value> ')' {@<token,{$2,null,$4}>}
	| <$xmlcreate>
	| true {@<bool,{"1"}>}
	| false {@<bool,{"0"}>}
	| null {@<setnull,{}>}
	| obj <$function> {@<obj,{$2}>}
	| '+' <$function>
	| '-' <$function> {@<neg,{$2}>}
	| '~' <$function> {@<inv,{$2}>}
	| '!' <$function> {@<not,{$2}>}
	| static <$comp> {@<static,{$2}>}
	| const <$comp> {@<const,{$2}>}
	| <custom_function>
	| <$litname> '(' ')' {@<call,{$1}>}
	| <$litname> '(' <$list> ')' {@<call,{$1,$3}>}
	| <$lit>
	;
@global["$compName"]
	= rule <$opcreate> {@<name,{"$",$1}>}
	| <$litname> '(' ')' {@<name,{$1,null}>}
	| <$litname> '(' <$list> ')' {@<name,{$1,$3}>}
	| <$litname>
	;
@global["$pathpart"]
	= rule '~' {@<lit,{"~"}>}
	| '.' '.' {@<lit,{".."}>}
	| '.' {@<lit,{"."}>}
	| <$litname>
	;
@global["$path"]
	= rule <$pathpart> '/' <$path> {@<add,{$1,"/",$3}>}
	| <$pathpart> '/'
	| <$pathpart>
	| '/'
	;
@global["$comppart"]
	= rule '[' <$comp> ']' <$comppart> {@<prepend,{$4,$2}>}
	| '[' <$comp> ']' {@<createlist,{$2}>}
	| '[' ']' <$comppart> {@<prepend,{$3,null}>}
	| '[' ']' {@<createlist,{null}>}
	| '.' <$compName> <$comppart> {@<prepend,{$3,$2}>}
	| '.' <$compName> {@<createlist,{$2}>}
	;
@global["$compL3"]
	= rule <$lit> <$comppart> {@<search,{$1,$2}>}
	| <$function> <$comppart> {@<search,{$1,$2}>}
	| <$function>
	;
@global["$compL2"]
	= rule <$compL3> '(' ')' {@<call,{$1}>}
	| <$compL3> '(' <$list> ')' {@<call,{$1,$3}>}
	| <$compL3>
	;
@global["$compExtend"]
	= rule <$compExtend> '+' '+' <$compL2> {@<extend,{$1,$4}>}
	| <$compExtend> '-' '-' <$compL2> {@<remove,{$1,$4}>}
	| <$compL2>
	;

/* https://en.cppreference.com/w/cpp/language/operator_precedence */
@global["$compPow"]
	= rule <$compPow> '*' '*' <$compExtend> {@<pow,{$1,$4}>}
	| <$compPow> '*' '/' <$compExtend> {@<root,{$1,$4}>}
	| <$compExtend>
	;
@global["$compMul"]
	= rule <$compMul> '*' <$compPow> {@<mul,{$1,$3}>}
	| <$compMul> '/' <$compPow> {@<div,{$1,$3}>}
	| <$compMul> '%' <$compPow> {@<mod,{$1,$3}>}
	| <$compMul> '.' '*' <$compPow> {@<dot,{$1,$4}>}
	| <$compPow>
	;
@global["$compAdd"]
	= rule <$compAdd> '+' <$compMul> {@<add,{$1,$3}>}
	| <$compAdd> '-' <$compMul> {@<sub,{$1,$3}>}
	| <$compMul>
	;
@global["$compsetBitShift"]
	= rule <$compsetBitShift> '<' '<' <$compAdd> {@<bsl,{$1,$4}>}
	| <$compsetBitShift> '>' '>' <$compAdd> {@<bsr,{$1,$4}>}
	| <$compAdd>
	;
@global["$compsetTW"]
	= rule <$compsetTW> '<' '=' '>' <$compsetBitShift> {@<cmp,{$1,$5}>}
	| <$compsetBitShift>
	;
@global["$compsetGtLt"]
	= rule <$compsetGtLt> '<' '=' <$compsetTW> {@<lteq,{$1,$4}>}
	| <$compsetGtLt> '<' <$compsetTW> {@<lt,{$1,$3}>}
	| <$compsetGtLt> '>' '=' <$compsetTW> {@<gteq,{$1,$4}>}
	| <$compsetGtLt> '>' <$compsetTW> {@<gt,{$1,$3}>}
	| <$compsetTW>
	;
@global["$compsetEqNEq"]
	= rule <$compsetEqNEq> '=' '=' <$compsetGtLt> {@<eq,{$1,$4}>}
	| <$compsetEqNEq> '!' '=' <$compsetGtLt> {@<neq,{$1,$4}>}
	| <$compsetGtLt>
	;
@global["$compsetBitAnd"]
	= rule <$compsetEqNEq> '&' <$compsetBitAnd> {@<band,{$1,$3}>}
	| <$compsetEqNEq>
	;
@global["$compsetBitXOr"]
	= rule <$compsetBitAnd> '^' <$compsetBitXOr> {@<xor,{$1,$3}>}
	| <$compsetBitAnd>
	;
@global["$compsetBitOr"]
	= rule <$compsetBitXOr> '|' <$compsetBitOr> {@<bor,{$1,$3}>}
	| <$compsetBitXOr>
	;
@global["$compsetAnd"]
	= rule <$compsetBitOr> '&' '&' <$compsetAnd> {@<and,{$1,$4}>}
	| <$compsetBitOr>
	;
@global["$compsetOr"]
	= rule <$compsetAnd> '|' '|' <$compsetOr> {@<or,{$1,$4}>}
	| <$compsetAnd>
	;
@global["$compT"]
	= rule <$compsetOr> '?' ':' <$comp> {@<if,{$1,null,$4}>}
	| <$compsetOr> '?' <$compsetOr> ':' <$compsetOr> ':' <$comp> {@<if,{$1,$3,$5,$7}>}
	| <$compsetOr> '?' <$compsetOr> ':' <$comp> {@<if,{$1,$3,$5}>}
	| <$compsetOr>
	;
@global["$comp"]
	= rule <$litname> ':' <$comp> {@<name,{$1,$3}>}
	| <$compT> ':' <$comp> {@<name,{$1,$3}>}
	| <$compT>
	;
@global["$else"]
	= rule elseif '(' <$comp> ')' <$command> ';' <$else> {@<if,{$3,$5,$7}>}
	| elseif '(' <$comp> ')' <$command> <$else> {@<if,{$3,$5,$6}>}
	| elseif '(' <$comp> ')' <$command> ';' {@<if,{$3,$5}>}
	| elseif '(' <$comp> ')' <$command> {@<if,{$3,$5}>}
	| else <$command>
	;
@global["$command"]
	= rule '{' <$command_list> ';' '}' {@<scope,{$2}>}
	| $SYSID("INCLUDE") <$comp> {@<include,{$2}>}
	| include <$comp> {@<include,{$2}>}
	| $SYSID("RESET") {@<reset,{}>}
	| reset {@<reset,{}>}
	| if '(' <$comp> ')' <$command> ';' <$else> {@<if,{$3,$5,$7}>}
	| if '(' <$comp> ')' <$command> <$else> {@<if,{$3,$5,$6}>}
	| if '(' <$comp> ')' <$command> {@<if,{$3,$5}>}
	| while '(' <$comp> ')' <$command> {@<while,{$3,$5}>}
	| switch '(' <$comp> ')' '{' <$case_list> '}' {@<switch,{$3,$6}>}
	| break {@<break,{}>}
	| return '(' <$comp> ')' {@<return,{$3}>}
	| exit {@<exit,{}>}
	| <custom_command>
	| <$litname> '=' <$comp> {@<assign,{$1,$3}>}
	| <$litname> '+' '=' <$comp> <$comp> {@<assignappend,{$1,$4,$5}>}
	| <$litname> '+' '=' <$comp> {@<assignappend,{$1,$4}>}
	| <$litname> '+' '+' '=' <$comp> <$comp> {@<assignextend,{$1,$5,$6}>}
	| <$litname> '+' '+' '=' <$comp> {@<assignextend,{$1,$5}>}
	| <$litname> '-' '=' <$comp> <$comp> {@<pop,{$1,$4,$5}>}
	| <$litname> '-' '=' <$comp> {@<pop,{$1,$4}>}
	| <$comp> '=' <$comp> {@<assign,{$1,$3}>}
	| <$comp> '+' '=' <$comp> <$comp> {@<assignappend,{$1,$4,$5}>}
	| <$comp> '+' '=' <$comp> {@<assignappend,{$1,$4}>}
	| <$comp> '+' '+' '=' <$comp> <$comp> {@<assignextend,{$1,$5,$6}>}
	| <$comp> '+' '+' '=' <$comp> {@<assignextend,{$1,$5}>}
	| <$comp> '-' '=' <$comp> <$comp> {@<pop,{$1,$4,$5}>}
	| <$comp> '-' '=' <$comp> {@<pop,{$1,$4}>}
	| <$comp>
	;
@global["$starcomment"]
	= rule ('/' '*') <> ('*' '/')
	;
@global["$command_list"]
	= rule <$command> <$starcomment> ';' <$command_list>
	| <$command> <$starcomment> <$command_list>
	| <$command> <$starcomment>
	| <$command> ';' <$command_list>
	| <$command> <$command_list>
	| <$command>
	| <$starcomment> ';' <$command_list>
	| <$starcomment> <$command_list>
	| <$starcomment>
	;
@global["$start"]
	= rule <$command_list> $SYSID("BE")
	| <$command_list> ';'
	| <$command_list>
	;
